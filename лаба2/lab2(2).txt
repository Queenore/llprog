T 56 K [директива IO2, установка адреса загрузки]

G K [директива IO2, фиксация начального адреса подпрограммы]
[0] A 3 F [формирование кода инструкции возврата в Acc]
[1] T 23 [return] @ [запись инструкции возврата]

[инициализация адресного поля инструкции чтения]
[2] A 0 [addr] F [загрузка в аккумулятор адреса 0-го элемента массива]
[3] A 13 [s1] @ [прибавляем код инструкции с нулевым полем адреса]
[4] T 13 [s1] @ [запись сформированной инструкции, обнуление аккумулятора]

[loop]
[5] A 1 F [загружаем счетчик необработанных элементов массива]
[6] S 24 [c1] @ [уменьшение значения счетчика на 1]
[7] G 22 [exit] @
[8] T 1 F [запись значения счетчика в ячейку 1]

[m1]
[9] H 4 F [копирование текущего значения результата в умножающий регистр]
[10] V 2 [x] F [умножение текущего значения результата на x]
[11] L 64 F [коррекция]
[12] L 64 F [коррекция]

[s1]
[13] A 0 [addr] F [прибавление текущего коэффициента к результату]

[w1]
[14] R 128 F [коррекция]
[15] R 128 F [коррекция]
[16] T 4 D [запись значения результата в ячейку 4]

[модификация адресного поля инструкции чтения]
[17] A 24 [с1] @ [загрузка в аккумулятор константы 1]
[18] L 0 D [сдвиг на 1 разряд влево]
[19] A 13 [s1] @ [прибавляем код инструкции, исполненной на предыдущем шаге]
[20] T 13 [s1] @ [записываем сформированную инструкцию в память]

[21] E 5 [loop] @

[exit]
[22] T 0 F [обнуление аккумулятора]

[return]
[23] E 0 F [инструкция возврата из подпрограммы]

[24] [c1] P 0 D [const = 1]

G K [директива IO2, фиксация начального адреса программы]
[0] X 0 F
[1] A 11 [addr] @ [адрес массива]
[2] T 0 F [запись адреса массива в ячейку 0, обнуление аккумулятора]
[3] A 12 [len] @ [длина массива]
[4] T 1 F [запись длины массива в ячейку 1, обнуление аккумулятора]
[5] A 13 [x] @
[6] T 2 F [инициализации ячейки памяти 2 значением x]
[7] T 4 F [выделение ячейки памяти для результата]
[8] A 9 @
[9] G 56 F
[10] Z 0 F

[11] P 14 [addr - номер ячейки 1-го элемента массива] @
[12] P 3 [len - длина массива] F
[13] P 2 [x] D
[14] P 1 D [значение коэффициента при x^5]
[15] P 6 F [значение коэффициента при x^4]
[16] P 3 F [значение коэффициента при x^3]
[17] P 1 D [значение коэффициента при x^2]
[18] P 0 F [значение коэффициента при x^1]
[19] P 4 D [значение свободного члена]
EZ PF [директива IO2, переход к исполнению]
